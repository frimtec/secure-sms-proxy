apply plugin: 'com.android.library'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'signing'

System.setProperty("line.separator", "\n")
Properties versionProps = new Properties()
def versionPropsFile = file('../app/version.properties')
if (versionPropsFile.exists()) {
    versionProps.load(new FileInputStream(versionPropsFile))
}
int vcode = Integer.parseInt((versionProps['VERSION_CODE'] == null || versionProps['VERSION_CODE'].startsWith('$')) ? String.valueOf((int) ((System.currentTimeMillis() / 1000 / 60) - 26_797_800)) : versionProps['VERSION_CODE'])
String vname = (versionProps['VERSION_NAME'] == null || versionProps['VERSION_NAME'].startsWith('$')) ? '3.0.0-dev' : versionProps['VERSION_NAME']

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''

ext["signing.keyId"] = System.getenv('SIGNING_KEYID')
ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_RINGFILE')
ext["ossUsername"] = System.getenv('OSS_USERNAME')
ext["ossPassword"] = System.getenv('OSS_PASSWORD')

jacoco {
    toolVersion = "0.8.7"
}

android {
    compileSdk 34

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
        buildConfigField 'int', 'VERSION_CODE', "${vcode}"
        buildConfigField 'String', 'VERSION_NAME', "\"${vname}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    namespace 'com.github.frimtec.android.securesmsproxyapi'
}

tasks.register('sourceJar', Jar) {
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        release(MavenPublication) {
            groupId 'com.github.frimtec'
            artifactId 'secure-sms-proxy-api'
            version vname
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/securesmsproxyapi-release.aar")
            pom {
                name = 'Secure SMS Proxy API'
                description = 'API to integrate S2MSP (Secure SMS Proxy) into an android application.'
                url = 'https://github.com/frimtec/secure-sms-proxy'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'frimtec'
                        name = 'Markus Friedli'
                        email = 'frimtec@gmx.ch'
                        url = 'https://github.com/frimtec'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:frimtec/secure-sms-proxy'
                    developerConnection = 'scm:git:git@github.com:frimtec/secure-sms-proxy'
                    url = 'https://github.com/frimtec/secure-sms-proxy'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/frimtec/secure-sms-proxy/issues'
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username ossUsername
                password ossPassword
            }
        }
    }
}

signing {
    sign publishing.publications
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.vdurmont:semver4j:3.1.0'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.11.3"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.11.3"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.11.4"
    testImplementation "org.assertj:assertj-core:3.26.3"
    testImplementation 'org.json:json:20240303'
    testImplementation "org.mockito:mockito-core:5.14.2"
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

