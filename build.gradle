buildscript {
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.6.2.0"
        classpath 'com.hiya:jacoco-android:0.2'
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: 'jacoco'

task jacocoMergeAll(type: JacocoMerge) {
    executionData = files('app/build/jacoco/testDebugUnitTest.exec', 'securesmsproxyapi/build/jacoco/testDebugUnitTest.exec')
}

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn jacocoMergeAll

    sourceDirectories.from =
            files('app/src/main/java', 'securesmsproxyapi/src/main/java')
    additionalSourceDirs.from =
            files('app/src/main/java', 'securesmsproxyapi/src/main/java')
    classDirectories.from =
            files('app/build/intermediates/javac/debug/classes', 'securesmsproxyapi/build/intermediates/javac/debug/classes')
    executionData.from =
            files("${buildDir}/jacoco/jacocoMergeAll.exec")
    reports {
        html.enabled = true
        xml.enabled = true
    }
}

tasks.coveralls {
    group = 'Coverage reports'
    description = 'Uploads the aggregated coverage report to Coveralls'

    dependsOn jacocoRootReport
}


coveralls {
    jacocoReportPath 'build/reports/jacoco/jacocoRootReport/jacocoRootReport.xml'
    sourceDirs += ['app/src/main/java', 'securesmsproxyapi/src/main/java']
}